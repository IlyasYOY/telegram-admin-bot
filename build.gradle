plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'

    id 'java'
    
    id "com.diffplug.spotless" version "5.14.2"
    id "jacoco"
}

group = 'ru.ilyasyoy.telegram.admin'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    telegramBotsVersion = '5.3.0'
    easyRandomVersion = "5.0.0"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation "org.telegram:telegrambots-spring-boot-starter:${telegramBotsVersion}"
    implementation "org.telegram:telegrambotsextensions:${telegramBotsVersion}"
    implementation "org.telegram:telegrambots-chat-session-bot:${telegramBotsVersion}"
    implementation "org.telegram:telegrambots-meta:${telegramBotsVersion}"
    implementation "org.telegram:telegrambots-abilities:${telegramBotsVersion}"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.jeasy:easy-random-core:${easyRandomVersion}"
}

spotless {
    java {
        importOrder() // standard import order
        removeUnusedImports()

        googleJavaFormat().aosp() // has its own section below
      }
}

test {
    useJUnitPlatform()
    
    finalizedBy([jacocoTestReport])
}

jacocoTestReport {
    reports {
        xml.required = true
        // for ryanluker.vscode-coverage-gutters
        xml.outputLocation = project.file('cov.xml')
    }
    dependsOn test
}

build {
    dependsOn([
        jacocoTestCoverageVerification, 
        jacocoTestReport])
}


jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}